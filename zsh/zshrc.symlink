# Path to your oh-my-zsh installation.
export ZSH=~/.oh-my-zsh

# shortcut to this dotfiles path is $ZSHDOT
export ZSHDOT=~/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/IntelliJProjects

# set locale to chinese
export LANG="zh_CN.UTF-8"

# directories to be ignored in source process
directory_ignored=(zsh/oh-my-zsh functions)

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# load dotfile functions
# fpath=($ZSHDOT/functions $fpath)
# autoload -U $ZSHDOT/functions/*(:t)

# load helper functions
source $ZSHDOT/functions/*.zsh

# all of our zsh files
typeset -U config_files
config_files=($ZSHDOT/**/*.zsh)

# filter out ignored folder
for file in $directory_ignored
do
  pattern="$ZSHDOT/${file}/*"
  config_files=(${config_files:#$~pattern})
done

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
# here will also load oh-my-zsh file, and leverage fpath
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
# autoload -U compinit
# compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
export JAVA_HOME=/usr/lib/jvm/default-java
export APACHE_HOME=~/Apache
export HADOOP_HOME=${APACHE_HOME}/hadoop
export SPARK_HOME=${APACHE_HOME}/spark
export HIVE_HOME=${APACHE_HOME}/hive
export PATH=${HADOOP_HOME}/bin:$SPARK_HOME/bin:$HIVE_HOME/bin:$PATH
